;+
;
;
; For a given halo mass, redshift and  cosmology, find an estimate of virial radius,
; velocity following Bryan and Norman 1998
;
; halo_mass         halo mass in Msun
; red               redshift 
; EXTRA             cosmology
; H0                hubble constant
;
;
;-


pro cosm_virialrel, halo_mass, red, _EXTRA=EXTRA, H0=H0, silent=silent, out=out

  common cosmolgy_cmmn, cosm_dm, cosm_K, cosm_h, cosm_Ob, cosm_L, cosm_r, sigma_8
  common cosmolgy_other, omega_baryon

  ;;set intialize cosmology
  m_cosm_common, H0=h0, _EXTRA=extra, SILENT=silent

  ;;derive expansion factor
  aexp=1./(1+red)
    if ~keyword_set(silent) then splog, 'Expansion factor ', aexp

  ;;find cosmology relation 
  little_h=(cosm_h/100.)
  Ez_square=cosm_dm*(1+red)^3+cosm_L       ;;neglect Omega_r
  Hz=cosm_h*sqrt(Ez_square)                ;;km/s/Mpc
  Omega_mz=cosm_dm*(1+red)^3/Ez_square
  x=Omega_mz-1
  Delta_crit=(18.*!pi^2+82*x-39*x^2)
  
  if ~keyword_set(silent) then begin
     splog, 'Virial overdensity ', Delta_crit 
     splog, 'H(z) ', Hz
     splog, 'Omega_m(z) ', Omega_mz
  endif

  ;;find critical density
  rho_crit=1.88D-29*little_h^2*Ez_square   ;;g/cm^3
  rho_crit_zero=1.88D-29*little_h^2        ;;g/cm^3

  if ~keyword_set(silent) then begin
     splog, 'Critical mass density at z (g/cm^3) ', rho_crit
     splog, 'Critical mass density at z=0 (g/cm^3) ', rho_crit_zero
  endif
  
  ;;find mean baryon density 
  rho_mean_zero=rho_crit_zero*omega_baryon
  nh_mean_zero=rho_mean_zero/(1.67262158D-24*1.3)  
  
  rho_mean=rho_mean_zero*(1+red)^3
  nh_mean=nh_mean_zero*(1+red)^3

  if ~keyword_set(silent) then begin
     splog, 'Mean baryon density at z (g/cm^3) ', rho_mean
     splog, 'Mean baryon number density at z (1/cm^3) ', nh_mean
     splog, 'Mean baryon density at z=0 (g/cm^3) ', rho_mean_zero
     splog, 'Mean baryon number density at z=0 (1/cm^3) ', nh_mean_zero
  endif

  ;;find virial radius
  halo_mass_g=halo_mass*1.98892D33 ;;gram
  rvir_cm=((3*halo_mass_g)/(4*!pi*Delta_crit*rho_crit))^(1./3)  ;;cm 
  rvir=rvir_cm*3.24077649D-22  ;;kpc
  if ~keyword_set(silent) then splog, 'Virial radius (kpc) ', rvir

  ;;find virial velocity
  v_circ=sqrt(6.6726D-8*halo_mass_g/rvir_cm)*1D-5 ;;km/s
  if ~keyword_set(silent) then splog, 'Virial velocity (km/s) ', v_circ

  ;;find virial temperature 
  tvir=0.5*(v_circ*1d5)^2*(1.672623d-24/1.380658d-16)
  if ~keyword_set(silent) then splog, 'Virial temperature (K) ', tvir
  
  ;;init output
  out={aexp:aexp,Delta_crit:Delta_crit,Hz:Hz,Omega_mz:Omega_mz,$
       rho_crit:rho_crit,rho_crit_zero:rho_crit_zero,rho_mean:rho_mean,$
       nh_mean:nh_mean,rho_mean_zero:rho_mean_zero,nh_mean_zero:nh_mean_zero,$
       rvir:rvir, v_circ:v_circ, tvir:tvir}

end


