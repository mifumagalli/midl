;+
;
; Apply an tweak the gain in LRIS images.
; Also scale the left chip to the level of the right one. This may not
; be optimal for the noise properties but helps to calibrate everyting
; to the sensitivity of the right CCD which is the best one.
;
;
;-



pro img_lris_gain, adu_image, electron_image, outgain=outgain, outscale=outscale, side=side

common ccdinfo, xfull,yfull,nchip,namp,biaslev,satur, goodata



  ;;define gain value as from lris page
if(side eq 'R') then begin
    ;;gain value for Amp1,2,3,4 (Jan 2010)
    ;;[1.022,0.955,0.877,0.916]
    ;;-
    ;;Current orientation of the image is 
    ;;[VidInp2,VidInp1;VidInp4,VidInp3]
    gain_value=[0.955,1.022,0.916,0.877]
endif


if(side eq 'B') then begin
    ;;gain value for Amp1,2,3,4 (Jan 2010)
    ;;[1.55,1.56,1.63,1.70]
    ;;-
    ;;Current orientation of the image is 
    ;;[Amp1,2,3,4]
    gain_value=[1.55,1.56,1.63,1.70]
endif

;;tweak the gain to level the two ampli in each chip
;;--------------------------------------------------

med_amp=fltarr(namp)
  
  for amp=0, namp-1 do begin
     xs=1024*amp
     xe=1024*(amp+1)-1
     ;;consider only data regions [430:3750,740:3160] 
     if(xs LT goodata[0]) then xs=goodata[0]
     if(xe GT goodata[1]) then xe=goodata[1]
     ;;try to mask bright stuff
     med_amp[amp]=djs_median(adu_image[xs:xe,goodata[2]:goodata[3]]*gain_value[amp])
  endfor


  ;;correct
  splog, 'Tweak gain... Corr_12 :', med_amp[0]/med_amp[1]
  splog, 'Tweak gain... Corr_34 :', med_amp[2]/med_amp[3]
  
  ;;check if resonable 
  if(med_amp[0]/med_amp[1] gt 1.5 or med_amp[0]/med_amp[1] lt 0.5) then begin
      splog, 'Variation for chips ', 1, ' bigger than 50%!'
      splog, 'Restore gain ', gain_value[1]
      med_amp[0]=1.
      med_amp[1]=1.
  endif
  
  if(med_amp[2]/med_amp[3] gt 1.5 or med_amp[2]/med_amp[3] lt 0.5) then begin
      splog, 'Variation for chips ', 2, ' bigger than 50%!'
      splog, 'Restore gain ', gain_value[3]
      med_amp[2]=1.
      med_amp[3]=1.
  endif
  
  gain_value[1]=gain_value[1]*med_amp[0]/med_amp[1]
  gain_value[3]=gain_value[3]*med_amp[2]/med_amp[3]
  
  
  ;;apply final gain
  electron_image=adu_image
  for amp=0, namp-1 do begin
      xs=1024*amp
      xe=1024*(amp+1)-1
      electron_image[xs:xe,0:yfull-1]=adu_image[xs:xe,0:yfull-1]*gain_value[amp]
      ;;consider only data regions [430:3750,740:3160] 
      if(xs LT goodata[0]) then xs=goodata[0]
      if(xe GT goodata[1]) then xe=goodata[1]
      med_amp[amp]=djs_median(electron_image[xs:xe,goodata[2]:goodata[3]])
  endfor
  
  ;;compute the scale of the right/left CCD
  splog, "Scale level at the chip-gap ", med_amp[2]/med_amp[1]

  ;;apply scale
  scale_amp=[med_amp[2]/med_amp[1],med_amp[2]/med_amp[1],1.,1.]
  for amp=0, namp-1 do begin
      xs=1024*amp
      xe=1024*(amp+1)-1
      electron_image[xs:xe,0:yfull-1]=electron_image[xs:xe,0:yfull-1]*scale_amp[amp]
  endfor
  
  
  
  ;;set out gain/scale
  outgain=gain_value
  outscale=scale_amp


  
end
